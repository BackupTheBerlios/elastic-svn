dnl Process this file with autoconf to produce a configure script.

dnl This is required at the start; the name is the name of a file
dnl it should be seeing, to verify it is in the same directory.

AC_INIT(dftables.c)

dnl Provide the current PCRE version information. Do not use numbers
dnl with leading zeros for the minor version, as they end up in a C
dnl macro, and may be treated as octal constants. Stick to single
dnl digits for minor numbers less than 10. There are unlikely to be
dnl that many releases anyway.

PCRE_MAJOR=3
PCRE_MINOR=5
PCRE_DATE=15-Aug-2001
PCRE_VERSION=${PCRE_MAJOR}.${PCRE_MINOR}
VERSION=$PCRE_VERSION
PACKAGE=pcre

AM_INIT_AUTOMAKE($PACKAGE, $VERSION, no-define)

dnl Arrange to build config.h from config.in. Note that pcre.h is
dnl built differently, as it is just a "substitution" file.
dnl Manual says this macro should come right after AC_INIT.
AM_CONFIG_HEADER(config.h:config.in)

dnl Provide versioning information for libtool shared libraries that
dnl are built by default on Unix systems.

PCRE_LIB_VERSION=0:1:0
PCRE_POSIXLIB_VERSION=0:0:0

dnl Check for Cygwin32
AC_SUBST(CYGWIN)
AC_CYGWIN
AM_CONDITIONAL(CYGWIN, test x$CYGWIN = xyes)

dnl Check for Mingw32
AC_SUBST(MINGW32)
AC_MINGW32
AM_CONDITIONAL(MINGW32, test x$MINGW32 = xyes)

AM_CONDITIONAL(CYGWIN,  test x$CYGWIN = xyes)
AM_CONDITIONAL(MINGW32, test x$MINGW32 = xyes)
AM_CONDITIONAL(WIN32, test x$MINGW32 = xyes || test x$CYGWIN = xyes)

dnl Cross compilation
CROSSCOMPILING=$cross_compiling
AC_SUBST(CROSSCOMPILING)
AM_CONDITIONAL(CROSSCOMPILING, test x$cross_compiling = xyes)

dnl Checks for programs.

AC_PROG_CC
AC_PROG_INSTALL
dnl AC_PROG_RANLIB

dnl Checks for header files.

AC_HEADER_STDC
AC_CHECK_HEADERS(limits.h)

dnl Checks for typedefs, structures, and compiler characteristics.

AC_C_CONST
AC_TYPE_SIZE_T

dnl Checks for library functions.

AC_CHECK_FUNCS(bcopy memmove strerror)

AC_SUBST(UTF8)
AC_SUBST(NEWLINE)

AC_ARG_ENABLE(utf8,
[  --enable-utf8=[no/yes]          enable UTF8 support (incomplete) [default=yes]],,enable_utf8=yes)

AC_ARG_WITH(newline,
[  --with-newline=[CR/LF/specify]  use CR, LF or given value as newline [default=LF]],newline_val=$with_newline,newline_val="LF")

if test "x$enable_utf8" = "xyes"; then
  UTF8="-DSUPPORT_UTF8"
else
  UTF8=""
fi

if test "x$newline_val" = "xCR" -o "x$newline_val" = "xcr"; then
  NEWLINE="-DNEWLINE=13"
elif test "x$newline_val" = "xLF" -o "x$newline_val" = "xlf"; then
  NEWLINE="-DNEWLINE=10"
elif test "x$newline_val" != "x"; then
  NEWLINE="-DNEWLINE=${newline_val}"
elif test "x$MINGW32" = "xyes" -o "x$CYGWIN" = "xyes"; then
  NEWLINE="-DNEWLINE=13"
else
  NEWLINE="-DNEWLINE=10"
fi

dnl Now arrange to build libtool

AM_PROG_LIBTOOL

dnl "Export" these variables

AC_SUBST(HAVE_MEMMOVE)
AC_SUBST(HAVE_STRERROR)
AC_SUBST(PCRE_MAJOR)
AC_SUBST(PCRE_MINOR)
AC_SUBST(PCRE_DATE)
AC_SUBST(PCRE_VERSION)
AC_SUBST(PCRE_LIB_VERSION)
AC_SUBST(PCRE_POSIXLIB_VERSION)

dnl This must be last; it determines what files are written as well as config.h
AC_OUTPUT(Makefile pcre.h:pcre.in pcre-config:pcre-config.in RunTest:RunTest.in,[chmod a+x RunTest pcre-config])
